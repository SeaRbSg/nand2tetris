// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t]==1) out[t+1] = 0
 * else if (load[t]==1)  out[t+1] = in[t]
 * else if (inc[t]==1)   out[t+1] = out[t] + 1  (integer addition)
 * else                  out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Add16(a=outprev, b[0]=true, b[1..15]=false, out=add1);
    Mux16(a=outprev, b=add1, sel=inc, out=incremented);

    Mux16(a=incremented, b=in, sel=load, out=loadin);

    Mux16(a=loadin, b=false, sel=reset, out=reseted);

    Or(a=inc, b=load, out=loadp);
    Or(a=loadp, b=reset, out=loadpp);

    Register(in=reseted, load=loadpp, out=out, out=outprev);
}





// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
  static int memAddr;
  static int numCharsPerRow;

  /** Initializes the keyboard. */
  function void init() {
    let memAddr = 24576;
    let numCharsPerRow = 64;
    return;
  } 

  /**
   * Returns the ASCII code (as char) of the currently pressed key,
   * or 0 if no key is currently pressed.
   * Recognizes all ASCII characters, as well as the following extension
   * of action keys:
   * New line = 128 = String.newLine()
   * Backspace = 129 = String.backSpace()
   * Left Arrow = 130
   * Up Arrow = 131
   * Right Arrow = 132
   * Down Arrow = 133
   * Home = 134
   * End = 135
   * Page Up = 136
   * Page Down = 137
   * Insert = 138
   * Delete = 139
   * ESC = 140
   * F1 - F12 = 141 - 152
   */
  function char keyPressed() {
    return Memory.peek(memAddr);
  }

  /**								
   * Reads the next character from the keyboard.
   * waits until a key is pressed and then released, then echoes
   * the key to the screen, and returns the value of the pressed key.
   */
  function char readChar() {
    var char c;

    //    do Output.printChar(0);          // use black square as cursor

    while (c = 0) {
      let c = Keyboard.keyPressed();
    }

    while (~(Keyboard.keyPressed() = 0)) {
      // wait for the user to let go
    }
    //    do Output.backSpace();
    do Output.printChar(c);
    return c;
  }

  /**								
   * Prints the message on the screen, reads the next line
   * (until a newline character) from the keyboard, and returns its value.
   */
  function String readLine(String message) {
    var String s;
    var char c;
    var char newLine;
    var char backSpace;

    let s = String.new(numCharsPerRow);
    let newLine = String.newLine();
    let backSpace = String.backSpace();
    let c = 0;

    do Output.printString(message);

    let c = Keyboard.readChar();
    while (~(c = newLine)) {
      if (c = backSpace) {
        do s.eraseLastChar();
      }
      else {
        do s.appendChar(c);
      }

      let c = Keyboard.readChar();
    }

    return s;
  }   

  /**								
   * Prints the message on the screen, reads the next line
   * (until a newline character) from the keyboard, and returns its
   * integer value (until the first non numeric character).
   */
  function int readInt(String message) {
    var String s;
    let s = Keyboard.readLine(message);

    return s.intValue();
  }
}

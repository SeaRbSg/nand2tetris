// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
    // zero x based off of zx
    Mux16(a=x, b=false, sel=zx, out=x1);

    // negate x based off of nx
    Not16(in=x1, out=xp);
    Mux16(a=x1, b=xp, sel=nx, out=x2);

    // zero y based off of zy
    Mux16(a=y, b=false, sel=zy, out=y1);

    // negate y based off of ny
    Not16(in=y1, out=yp);
    Mux16(a=y1, b=yp, sel=ny, out=y2);

    // compute xy
    And16(a=x2, b=y2, out=xya);
    Add16(a=x2, b=y2, out=xyp);
    Mux16(a=xya, b=xyp, sel=f, out=xy);

    // negate output based off of no
    Not16(in=xy, out=xy1);
    Mux16(a=xy, b=xy1, sel=no, out=out, out[0..7]=io1, out[8..15]=io2, out[15]=ng);

    // set other out bits
    // zr
    Or8Way(in=io1, out=zr1);
    Or8Way(in=io2, out=zr2);
    Or(a=zr1, b=zr2, out=zr3);
    Not(in=zr3, out=zr);
}
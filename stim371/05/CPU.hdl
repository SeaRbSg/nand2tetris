// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Central Processing unit (CPU).
 * Consists of an ALU and a set of registers, designed to fetch and 
 * execute instructions written in the Hack machine language.
 * In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM=0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time unit. If reset=1 then the 
 * CPU jumps to address 0 (i.e. sets pc=0 in next time unit) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset=1) or continue executing
                         // the current program (reset=0).

    OUT outM[16],        // M value output
        writeM,          // Write into M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    
    //decode
    // i xx a cccccc ddd jjj

    // switch between D and A
    Mux16(a=instruction, b=ALUOut, sel=instruction[15], out=Adata);

    // control bit for ARegister
    Not(in=instruction[15],out=Ainst);
    Or(a=Ainst, b=instruction[5], out=loadA);
    ARegister(in=Adata, load=loadA, out[0..14]=addressM, out=AReg);

    // when to store in D
    And(a=instruction[4], b=instruction[15], out=loadD);
    DRegister(in=ALUOut, load=loadD, out=datareg);

    Mux16(a=AReg, b=inM, sel=instruction[12], out=AMswitch);

    ALU(x=datareg, y=AMswitch, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=ALUOut, zr=zero, ng=lessthan);

    //when to write out to memory
    And(a=instruction[15], b=instruction[3], out=writeM);

    // calculate positive attribute from ALU output
    Or(a=zero, b=lessthan, out=lte);
    Not(in=lte, out=positive);

    // comparing jump bits to ALU output
    And(a=instruction[2], b=lessthan, out=jlt);
    And(a=instruction[1], b=zero, out=jeq);
    And(a=instruction[0], b=positive, out=jgt);

    // reduce jump logic to single value (to jump or not to jump. that is the question)
    Or(a=jlt, b=jeq, out=tempj);
    Or(a=tempj, b=jgt, out=jumpor);
    And(a=instruction[15], b=jumpor, out=jump);

    PC(in=AReg, load=jump, inc=true, reset=reset, out[0..14]=pc, out[15]=abc);
}
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs,
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are
 * documented in the book. In addition, the ALU
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values,
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1


CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:

    Mux16(a=x, b=false, sel=zx, out=x2);
    Mux16(a=y, b=false, sel=zy, out=y2);

    // if (zx==1) set x = 0        // 16-bit constant
    // if (zy==1) set y = 0        // 16-bit constant
    Not16(in=x2, out=notx2);
    Not16(in=y2, out=noty2);

    // if (nx==1) set x = ~x       // bitwise "not"
    // if (ny==1) set y = ~y       // bitwise "not"
    Mux16(a=x2, b=notx2, sel=nx, out=x3);
    Mux16(a=y2, b=noty2, sel=ny, out=y3);

    // if (f==1)  set out = x + y  // integer 2's complement addition
    // if (f==0)  set out = x & y  // bitwise "and"
    Add16(a=x3, b=y3, out=add16);
    And16(a=x3, b=y3, out=and16);
    Mux16(a=and16, b=add16, sel=f, out=out1);

    // if (no==1) set out = ~out   // bitwise "not"
    // if (out<0) set ng = 1
    Not16(in=out1, out=notout);
    Mux16(a=out1, b=notout, sel=no, out=out, out[0..7]=out2a, out[8..15]=out2b, out[15]=ng);

    // if (out==0) set zr = 1
    Or8Way(in=out2a, out=outa);
    Or8Way(in=out2b, out=outb);
    Or(a=outa, b=outb, out=nzr);
    Not(in=nzr, out=zr);
}
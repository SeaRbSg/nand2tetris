// This file is part of the materials accompanying the book
// "The Elements of Computing Systems" by Nisan and Schocken,
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/b/RAM512.hdl

/**
 * Memory of 512 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load=1, then
 * the in value is loaded into the memory location specified by address
 * (the loaded value will be emitted to out after the next time step.)
 */

CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:

    // Send load bit to RAM64
    DMux8Way(in = load, sel = address[6..8], a = l0, b = l1, c = l2,
             d = l3, e = l4, f = l5, g = l6, h = l7);

    RAM64(in = in, load = l0, address = address[0..5], out = w0);
    RAM64(in = in, load = l1, address = address[0..5], out = w1);
    RAM64(in = in, load = l2, address = address[0..5], out = w2);
    RAM64(in = in, load = l3, address = address[0..5], out = w3);
    RAM64(in = in, load = l4, address = address[0..5], out = w4);
    RAM64(in = in, load = l5, address = address[0..5], out = w5);
    RAM64(in = in, load = l6, address = address[0..5], out = w6);
    RAM64(in = in, load = l7, address = address[0..5], out = w7);

    // Send RAM64 output to out
    Mux8Way16(a = w0, b = w1, c = w2, d = w3, e = w4, f = w5, g = w6,
              h = w7, sel = address[6..8], out = out);
}
